plugins {
    id 'org.asciidoctor.jvm.convert' version "3.3.2"
}

configurations {
    asciidoctorExtensions
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
//    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation project(':core-domain')
    testImplementation 'io.rest-assured:rest-assured:5.3.2'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
}

// snippets 파일이 저장될 경로 설정
ext {
    snippetsDir = file('build/generated-snippets')
}

// test 실행 시 snippets 저장할 경로로 snippetsDir 설정
test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

// API 문서 생성
asciidoctor {
    configurations 'asciidoctorExtensions'
    inputs.dir snippetsDir
    dependsOn test
}

bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/asciidoc") {
        into 'static/docs'
    }
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc/")
    into file("src/main/resources/static/docs")
}

build {
    dependsOn copyDocument
}
